@top Template { element* }

element { directive | Text }
conditionalElement { directive | ElseDirective | Text }

directive {
  Insert |
  ForDirective { ForOpen element* ForClose } |
  IfDirective { IfOpen conditionalElement* IfClose } |
  UnlessDirective { UnlessOpen conditionalElement* UnlessClose } |
  Comment
}

@skip {space} {
  Insert { startInsert DirectiveContent "}}" }
  ElseDirective { startElse DirectiveContent? "}}" }
  IfOpen { startIf DirectiveContent "}}" }
  IfClose { endIf "}}" }
  UnlessOpen { startUnless DirectiveContent "}}" }
  UnlessClose { endUnless "}}" }
  ForOpen { startFor DirectiveContent "}}" }
  ForClose { endFor "}}" }
}

startInsert { "{{" ":" | "{{" ">" }
startElse { "{{" "else" }
startIf { "{{" "if" }
endIf { "{{/" "if" }
startUnless { "{{" "unless" }
endUnless { "{{/" "unless" }
startFor { "{{" "for" }
endFor { "{{/" "for" }

@local tokens {
  commentEnd { "--}}" }
  @else commentContent
}

@skip {} {
  Comment { "{{!--" (commentContent)* commentEnd }
}

@tokens {
  Text { ![{] Text? | "{" (@eof | ![%{] Text?) }
  space { @whitespace+ }
  DirectiveContent { ![}] DirectiveContent? | "}" (@eof | ![}] DirectiveContent?) }
  @precedence { space DirectiveContent }
  "{{" "}}" "{{/" "if" "else" "for" "unless" ":" ">"
}
